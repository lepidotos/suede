dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/efs.h)

# libtool versioning
LT_CURRENT=1
LT_REVISION=0
LT_AGE=0

VERSION=$LT_CURRENT.$LT_REVISION.$LT_AGE
PACKAGE=libefs

AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)
AC_SUBST(VERSION)
AC_SUBST(PACKAGE)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

AC_ARG_WITH(html-dir, [  --with-html-dir=PATH path to installed docs ])
if test "x$with_html_dir" = "x" ; then
  HTML_DIR='${datadir}/libefs/html'
else
  HTML_DIR=$with_html_dir
fi
AC_SUBST(HTML_DIR)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AM_PROG_CC_STDC
AM_PROG_LIBTOOL

changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac

  case " $CXXFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CXXFLAGS="$CXXFLAGS -Wall" ;;
  esac

  if test "x$enable_ansi" = "xyes"; then
    case " $CFLAGS " in
    *[\ \	]-ansi[\ \	]*) ;;
    *) CFLAGS="$CFLAGS -ansi" ;;
    esac

    case " $CFLAGS " in
    *[\ \	]-pedantic[\ \	]*) ;;
    *) CFLAGS="$CFLAGS -pedantic" ;;
    esac
  fi
fi
changequote([,])dnl

dnl Checks for libraries.

AM_PATH_GLIB(1.2.1,, AC_MSG_ERROR(Test for GLIB failed))
#AM_PATH_GLIB_2_0(1.3.1,, AC_MSG_ERROR(Test for GLIB2 failed))

AC_CHECK_LIB(z, gzopen,
  AC_CHECK_HEADERS(zlib.h,, AC_MSG_ERROR(ZLIB headers not found)),
  AC_MSG_ERROR(ZLIB library not found))

CRYPTLIB=""
AC_CHECK_LIB(crypt, crypt, CRYPTLIB="-lcrypt", )
AC_SUBST(CRYPTLIB)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(crypt.h fcntl.h sys/file.h unistd.h)

dnl Try to determine DIR location and structure
AC_HEADER_DIRENT

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_STRUCT_ST_RDEV

dnl Checks for library functions.
AC_CHECK_FUNCS(gethostname strdup)

AC_OUTPUT(
	Makefile	
	src/Makefile
	doc/Makefile
	libefs-config
	libefs.pc
)	
