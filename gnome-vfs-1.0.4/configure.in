AC_INIT(libgnomevfs/gnome-vfs.h)
AM_INIT_AUTOMAKE(gnome-vfs, 1.0.4)

GNOME_COMMON_INIT

AM_PROG_XML_I18N_TOOLS

AM_CONFIG_HEADER(config.h)
AM_ACLOCAL_INCLUDE(macros)

AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_AWK
AM_PROG_LIBTOOL

PKG_CHECK_MODULES(MIME_DATABASE, gnome-mime-data-2.0)

dnl ==========================================================================
dnl
dnl If you add a version number here, you *must* either use it with
dnl EAZEL_VERSION_INSIST or add an AC_SUBST line for it, or it will
dnl never make it into the spec file!
dnl
dnl ==========================================================================

GLIB_REQUIRED=1.2.6
GCONF_REQUIRED=0.9
OAF_REQUIRED=0.3.0
XML_REQUIRED=1.8.8

AC_SUBST(GLIB_REQUIRED)
AC_SUBST(GCONF_REQUIRED)
AC_SUBST(OAF_REQUIRED)
AC_SUBST(XML_REQUIRED)

dnl ===========================================================================

dnl EAZEL_VERSION_CANON(version)
dnl                     1

AC_DEFUN(EAZEL_VERSION_CANON, [`

	dnl Assumes that there are no more than 999 revisions at a level,
	dnl no more than three levels of revision.
	dnl
	dnl Any more than that, and test starts messing up the numeric
	dnl comparisons because its integers overflow, and there's no
	dnl way to do string comparisons in the shell.  Grr.
	dnl
	dnl Must come up with some way to fix this.

	echo "$1" |
	tr . '\012' |
	sed -e 's/^/000/' -e 's/^.*\(...\)/\1/' |
	tr -d '\012' |
	sed 's/$/000000000/
	     s/^\(.........\).*/\1/'
`])

dnl EAZEL_VERSION_INSIST(package, get-version-cmd, operator, want-version-var)
dnl                      1        2                3         4

AC_DEFUN(EAZEL_VERSION_INSIST, [
	ez_want_version=[$]$4

	case "$3" in
		">")	ez_operator=-gt ;;
		">=")	ez_operator=-ge ;;
		"<")	ez_operator=-lt ;;
		"<=")	ez_operator=-le ;;
		"=")	ez_operator=-eq ;;
		"!=")	ez_operator=-ne ;;
		*)	AC_ERROR(Unknown operator $3 in configure script) ;;
	esac

	AC_MSG_CHECKING(for $1 $3 [$ez_want_version])

	if ez_installed_version="`$2`"
	then
		AC_MSG_RESULT([$ez_installed_version])
	else
		AC_ERROR($2 failed)
	fi

	if test "EAZEL_VERSION_CANON([$ez_installed_version])" "$ez_operator" \
		"EAZEL_VERSION_CANON([$ez_want_version])"
	then
		:
		AC_SUBST($4)
	else
		AC_ERROR($1 version [$ez_want_version] is required.)
	fi
])

dnl ===========================================================================

dnl ====================================
dnl =
dnl = Profiling support
dnl =
dnl ====================================
ENABLE_PROFILER=
AC_ARG_ENABLE(profiler,
[  --enable-profiler		Enable profiler],
ENABLE_PROFILER=1
AC_DEFINE(ENABLE_PROFILER))

if test "x$ENABLE_PROFILER" = "x1"
then
	CFLAGS="-g -O -gdwarf-2 -finstrument-functions -D__NO_STRING_INLINES"
	LDFLAGS="/gnome/PROFILE/lib/libprofiler.so -lpthread"
fi

AC_SUBST(ENABLE_PROFILER)
AM_CONDITIONAL(ENABLE_PROFILER, test "x$ENABLE_PROFILER" = "x1")
dnl ====================================
dnl = Profiling support
dnl ====================================

dnl Version of the GNOME Platform (1002000 for GNOME 1.2.x)
GNOME_PLATFORM_VERSION=1002000
AC_DEFINE_UNQUOTED(GNOME_PLATFORM_VERSION, $GNOME_PLATFORM_VERSION)
AM_CONDITIONAL(PLATFORM_GNOME_2, test $GNOME_PLATFORM_VERSION -ge 1095000)

dnl Init GNOME for the GNOME 1.2 platform
GNOME_INIT
VFS_GNOME_CFLAGS="`gnome-config --cflags gnome`"
VFS_GNOMEUI_CFLAGS="`gnome-config --cflags gnome gnomeui`"
VFS_GNOMEGNORBA_CFLAGS="`gnome-config --cflags gnome gnomeui gnorba`"
VFS_CAPPLET_CFLAGS="`gnome-config --cflags gnome gnomeui capplet`"
VFS_GNOME_LIBS="`gnome-config --libs gnome`"
VFS_GNOMEUI_LIBS="`gnome-config --libs gnome gnomeui`"
VFS_GNOMEGNORBA_LIBS="`gnome-config --libs gnome gnomeui gnorba`"
VFS_CAPPLET_LIBS="`gnome-config --libs gnome gnomeui capplet`"
VFS_GLIB_CONFIG="glib-config"
AC_SUBST(VFS_GNOME_CFLAGS)
AC_SUBST(VFS_GNOMEUI_CFLAGS)
AC_SUBST(VFS_GNOMEGNORBA_CFLAGS)
AC_SUBST(VFS_CAPPLET_CFLAGS)
AC_SUBST(VFS_GNOME_LIBS)
AC_SUBST(VFS_GNOMEUI_LIBS)
AC_SUBST(VFS_CAPPLET_LIBS)
AC_SUBST(VFS_GNOMEGNORBA_LIBS)

AC_ARG_ENABLE(gconf,
              [  --enable-gconf          Enable GConf access backend],
              gconf_enabled="yes")
AC_ARG_ENABLE(libefs,
              [  --disable-libefs={yes|no}       Disable libefs access backend],
              libefs_disabled="yes")
AC_ARG_WITH(default-backend,
	      [  --with-default-backend=(corba|pthread)		    ],
	      default_backend="$withval")

AM_PATH_GLIB($GLIB_REQUIRED,,
  AC_MSG_ERROR([
*** GLIB 1.2.0 or better is required. The latest version of GLIB
*** is always available from ftp://ftp.gtk.org/.]),
    gmodule gthread)
AC_SUBST(GLIB_LIBS)
AC_SUBST(GLIB_CFLAGS)

AC_DEFINE(USING_OAF)
AC_SUBST(OAF_LIBS)
AC_SUBST(OAF_CFLAGS)

AC_DEFINE(HAVE_GCONF)

AM_PATH_GTK(1.2.0,,
  AC_MSG_ERROR([
*** Gtk+ 1.2.0 or better is required. The latest version of Gtk+
*** is always available from ftp://ftp.gtk.org/.]),
    gmodule gthread)

cflags_set=yes
GNOME_COMPILE_WARNINGS
VFS_CFLAGS="$warnCFLAGS $complCFLAGS"

GNOME_PTHREAD_CHECK
dnl semaphore functions are in librt.so on solaris
AC_CHECK_LIB(rt, sem_wait)

dnl Don't blindly #define them if they're typedef'ed in <sys/types.h>
AM_GNOME_SIZE_T
AM_GNOME_OFF_T

ALL_LINGUAS="az ca da de el es eu fi fr ga gl hu it ja ko lt nl nn no pl pt pt_BR ro ru sk sl sv tr uk wa zh_TW"
AM_GNOME_GETTEXT

AC_FUNC_ALLOCA
AC_CHECK_FUNCS(getdtablesize open64 lseek64 statvfs)
AC_REPLACE_FUNCS(getdelim)

dnl
dnl This is so that we find the 64bit type
dnl
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(long long)

VFS_SIZE="unsigned long"
VFS_OFFSET="long"
VFS_SIZE_IS="UNSIGNED_LONG"
VFS_OFFSET_IS="LONG"
VFS_SIZE_PRINTF="lu"
VFS_OFFSET_PRINTF="ld"

dnl
dnl See if any of the types is 64 bits, and in case it is, use it
dnl Note: We test an (int) even though I think using (long) would be fine.
dnl       This is because glib does it this way for gint64 and so there
dnl       is probably a reason behind it
dnl
case 8 in
$ac_cv_sizeof_int)
  VFS_SIZE="unsigned int"
  VFS_OFFSET="int"
  VFS_SIZE_IS="UNSIGNED_INT"
  VFS_OFFSET_IS="INT"
  VFS_SIZE_PRINTF="u"
  VFS_OFFSET_PRINTF="d"
  ;;
$ac_cv_sizeof_long)
  # Nothing happens here, we are already using a long
  ;;
$ac_cv_sizeof_long_long)
  VFS_SIZE="unsigned long long"
  VFS_OFFSET="long long"
  VFS_SIZE_IS="UNSIGNED_LONG_LONG"
  VFS_OFFSET_IS="LONG_LONG"
  VFS_SIZE_PRINTF="Lu"
  VFS_OFFSET_PRINTF="Ld"
  ;;
esac

AC_SUBST(VFS_SIZE)
AC_SUBST(VFS_OFFSET)
AC_SUBST(VFS_SIZE_IS)
AC_SUBST(VFS_OFFSET_IS)
AC_SUBST(VFS_SIZE_PRINTF)
AC_SUBST(VFS_OFFSET_PRINTF)

AC_CHECK_HEADERS(sys/resource.h sys/vfs.h sys/mount.h sys/statvfs.h)

dnl turn warnings into errors to enforce clean code

AC_ARG_ENABLE(more-warnings,
[  --enable-more-warnings  Maximum compiler warnings],
set_more_warnings="$enableval",[
if test -f $srcdir/CVSVERSION; then
	set_more_warnings=yes
else
	set_more_warnings=no
fi
])

WERROR=""
if test "$GCC" = "yes" -a "$set_more_warnings" != "no"; then
	echo "enable compile warnings = $set_more_warnings"
	VFS_CFLAGS="-Wall \
	-Wchar-subscripts -Wmissing-declarations -Wmissing-prototypes \
	-Wnested-externs -Wpointer-arith"
	WERROR="-Werror"

	for option in -Wsign-promo -Wno-sign-compare; do
		SAVE_CFLAGS="$CFLAGS"
		CFLAGS="$VFS_CFLAGS $option $CFLAGS"
		AC_MSG_CHECKING([whether gcc understands $option])
		AC_TRY_COMPILE([], [],
			has_option=yes,
			has_option=no,)
		CFLAGS="$SAVE_CFLAGS"
		if test $has_option = yes; then
		  VFS_CFLAGS="$VFS_CFLAGS $option"
		fi
		AC_MSG_RESULT($has_option)
		unset has_option
		unset SAVE_CFLAGS
	done
	unset option
fi
AC_SUBST(WERROR)
AC_SUBST(VFS_CFLAGS)

using_oaf=no
AC_PATH_PROG(OAF_CONFIG,oaf-config,no)
if test x$OAF_CONFIG = xno; then
	AC_MSG_RESULT("The program oaf-config was not found")
	OAF_LIBS=""
	OAF_CFLAGS=""
else
	EAZEL_VERSION_INSIST(oaf, $OAF_CONFIG --version, >=, OAF_REQUIRED)
	using_oaf=yes
	AC_DEFINE(USING_OAF)
	OAF_LIBS=`$OAF_CONFIG --libs`
	OAF_CFLAGS=`$OAF_CONFIG --cflags`
fi
AM_CONDITIONAL(USING_OAF, test x"$using_oaf" = "xyes")
AC_SUBST(OAF_LIBS)
AC_SUBST(OAF_CFLAGS)

dnl
dnl gnome vfs Documentation
dnl 
GNOME_VFS_DIR=`(cd $srcdir; pwd)`
AC_SUBST(GNOME_VFS_DIR)

AM_CONDITIONAL(INSIDE_GNOME_DOCU, false)

dnl
dnl gnome-xml libraries - for WebDAV support in modules/http-method.c
dnl

AC_PATH_PROG(XML_CONFIG,xml-config,no)
if test x$XML_CONFIG = "xno"; then
  AC_MSG_ERROR(Couldn't find xml-config please install the gnome-xml package)
fi

XML_VERSION="`$XML_CONFIG --version`"
case "$XML_VERSION" in
	1.*)
		:
  	;;

	*)
		AC_ERROR(gnome-vfs needs gnome-xml version 1.x not $XML_VERSION)
	;;
esac


XML_LIBS=`$XML_CONFIG --libs`
XML_CFLAGS=`$XML_CONFIG --cflags`
AC_SUBST(XML_LIBS)
AC_SUBST(XML_CFLAGS)

dnl
dnl gconf
dnl

AM_PATH_GCONF($GCONF_REQUIRED,,,gconf gconf-gtk)
AC_SUBST(GCONF_LIBS)
AC_SUBST(GCONF_CFLAGS)
AC_SUBST(GTK_LIBS)
AC_SUBST(GTK_CFLAGS)

dnl
dnl efs-method.c
dnl

dnl if test "x$libefs_disabled" = xyes; then 
dnl     LIBEFS_CONFIG=no
dnl else
dnl     AC_PATH_PROG(LIBEFS_CONFIG, libefs-config, no)
dnl fi
dnl 
dnl if test "x$LIBEFS_CONFIG" != xno; then
dnl 	LIBEFS_LIBS=`$LIBEFS_CONFIG --libs`
dnl 	LIBEFS_CFLAGS=`$LIBEFS_CONFIG --cflags`
dnl fi
dnl 

AM_CONDITIONAL(HAVE_LIBEFS, false) 
LIBEFS_LIBS=""
LIBEFS_CFLAGS=""
AC_SUBST(LIBEFS_LIBS)
AC_SUBST(LIBEFS_CFLAGS)

dnl
dnl ftp-method.c
dnl
AC_DEFUN(AC_STRUCT_LINGER, [
av_struct_linger=no
AC_MSG_CHECKING(struct linger is available)
AC_TRY_RUN([
#include <sys/types.h>
#include <sys/socket.h>

struct linger li;

main ()
{
    li.l_onoff = 1;
    li.l_linger = 120;
    exit (0);
}
],[
AC_DEFINE(HAVE_STRUCT_LINGER)
av_struct_linger=yes
],[
av_struct_linger=no
],[
av_struct_linger=no
])
AC_MSG_RESULT($av_struct_linger)
])

AC_CHECK_LIB(nsl, t_accept)
AC_CHECK_LIB(socket, socket)

have_socket=no
AC_CHECK_FUNCS(socket, have_socket=yes)
if test $have_socket = no; then
  # socket is not in the default libraries.  See if it's in some other.
  for lib in bsd socket inet; do
    AC_CHECK_LIB($lib, socket, [
	  LIBS="$LIBS -l$lib"
	  have_socket=yes
	  AC_DEFINE(HAVE_SOCKET)
	  break])
  done
fi

have_gethostbyname=no
AC_CHECK_FUNC(gethostbyname, have_gethostbyname=yes)
if test $have_gethostbyname = no; then
  # gethostbyname is not in the default libraries.  See if it's in some other.
  for lib in bsd socket inet; do
    AC_CHECK_LIB($lib, gethostbyname, [LIBS="$LIBS -l$lib"; have_gethostbyname=yes; break])
  done
fi

AC_CHECK_LIB(bz2, bzCompressInit, AC_DEFINE(HAVE_OLDER_BZIP2))

if test $have_socket = yes; then
    AC_STRUCT_LINGER
fi

dnl modules/cdda

AM_CONDITIONAL(HAVE_CDDA, false);
AC_CHECK_HEADERS(cdda_interface.h cdda_paranoia.h, [
	CDDA_LIBS="$CDDA_LIBS -lcdda_paranoia, -lcdda_interface";
		AM_CONDITIONAL(HAVE_CDDA, true);
	])

dnl ******************************
dnl OpenSSL
dnl ******************************

AC_ARG_WITH(openssl-includes, [  --with-openssl-includes=PREFIX     Location of OpenSSL includes.],
	with_openssl_includes="$withval", with_openssl_includes="/usr/include")
have_openssl_includes="no"
if test "x${with_openssl_includes}" != "xno"; then
	CPPFLAGS_save="$CPPFLAGS"
	
	AC_MSG_CHECKING(for OpenSSL includes)
	AC_MSG_RESULT("")
	
	CPPFLAGS="$CPPFLAGS -I$with_openssl_includes"
	AC_CHECK_HEADERS(openssl/ssl.h openssl/x509.h, [ openssl_includes="yes" ])
	CPPFLAGS="$CPPFLAGS_save"
	
	if test "x{$openssl_includes}" != "xno" -a "x{$openssl_includes}" != "x"; then
		have_openssl_includes="yes"
		OPENSSL_CFLAGS="-I$with_openssl_includes"
	else
		OPENSSL_CFLAGS=""
	fi
else
	AC_MSG_CHECKING(for OpenSSL includes)
	AC_MSG_RESULT(no)
fi

AC_ARG_WITH(openssl-libs, [  --with-openssl-libs=PREFIX     	Location of OpenSSL libs.],
	with_openssl_libs="$withval", with_openssl_libs="/usr/lib")
msg_openssl="no"
if test "x${with_openssl_libs}" != "xno" -a "x${have_openssl_includes}" != "xno"; then
	LDFLAGS_save="$LDFLAGS"
	
	AC_CACHE_CHECK([for OpenSSL libraries], openssl_libs,
	[
dnl How can we test to see if we need to link to libld for sure?? 
dnl RedHat 6.2 seems to need to but Debian Woody doesn't
		LDFLAGS="$LDFLAGS -L$with_openssl_libs -lssl -lcrypto -ldl"
		AC_TRY_LINK_FUNC(SSL_read, openssl_libs="yes", openssl_libs="no")
		LDFLAGS="$LDFLAGS_save"
	])
	if test "x${openssl_libs}" != "xno"; then
		AC_DEFINE(HAVE_OPENSSL)
		msg_openssl="yes"
		OPENSSL_LDFLAGS="-L$with_openssl_libs -lssl -lcrypto -ldl"
		AM_CONDITIONAL(HAVE_SSL, true)
	else
		OPENSSL_CFLAGS=""
		OPENSSL_LDFLAGS=""
		AM_CONDITIONAL(HAVE_SSL, false)
	fi
else
	AC_MSG_CHECKING(for OpenSSL libraries)
	AC_MSG_RESULT(no)
fi

AC_SUBST(OPENSSL_CFLAGS)
AC_SUBST(OPENSSL_LDFLAGS)



  
dnl **************************
dnl *** Checks for gtk-doc ***
dnl **************************

AC_ARG_WITH(html-dir, [  --with-html-dir=PATH path to installed docs ])

if test "x$with_html_dir" = "x" ; then
  HTML_DIR='${datadir}/gtk-doc/html'
else
  HTML_DIR=$with_html_dir
fi

AC_SUBST(HTML_DIR)

AC_CHECK_PROG(GTKDOC, gtkdoc-mkdb, true, false)

#gtk_doc_min_version=0.6
#if $GTKDOC ; then 
#    gtk_doc_version=`gtkdoc-mkdb --version`
#    AC_MSG_CHECKING([gtk-doc version ($gtk_doc_version) >= $gtk_doc_min_version])
#    if perl <<EOF ; then
#      exit (("$gtk_doc_version" =~ /^[[0-9]]+\.[[0-9]]+$/) &&
#            ("$gtk_doc_version" >= "$gtk_doc_min_version") ? 0 : 1);
#EOF
#      AC_MSG_RESULT(yes)
#   else
#      AC_MSG_RESULT(no)
#      GTKDOC=false
#   fi
#fi

AM_CONDITIONAL(HAVE_GTK_DOC, $GTKDOC)
AC_SUBST(HAVE_GTK_DOC)

AC_CHECK_PROG(DB2HTML, db2html, true, false)
AM_CONDITIONAL(HAVE_DOCBOOK, $DB2HTML)

dnl Let people disable the gtk-doc stuff.
AC_ARG_ENABLE(gtk-doc, [  --enable-gtk-doc  Use gtk-doc to build documentation [default=auto]], enable_gtk_doc="$enableval", enable_gtk_doc=auto)

if test "x$enable_gtk_doc" = xauto ; then
  if test "x$GTKDOC" = xtrue ; then
    enable_gtk_doc=yes
  else
    enable_gtk_doc=no 
  fi
fi

if test "x$enable_gtk_doc" = xyes; then
	ENABLE_GTK_DOC=true
else
 	ENABLE_GTK_DOC=false
fi


dnl NOTE: We need to use a separate automake conditional for this
dnl       to make this work with the tarballs.
AM_CONDITIONAL(ENABLE_GTK_DOC, $ENABLE_GTK_DOC)



dnl Final touch

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl This is needed by `test/vfs-run.in'
TOP_BUILDDIR="`pwd`"
AC_SUBST(TOP_BUILDDIR)

dnl
dnl Stuff for vfsConf.sh
dnl

VFS_LIBS="-lgnomevfs $XML_LIBS $OAF_LIBS $GCONF_LIBS $GLIB_LIBS"
VFS_LIBDIR="-L${libdir}"
VFS_INCLUDEDIR="-I${includedir}/gnome-vfs-1.0 -I${libdir}/gnome-vfs-1.0/include $XML_CFLAGS $OAF_CFLAGS $GCONF_CFLAGS $GLIB_CFLAGS"

AC_SUBST(VFS_LIBDIR)
AC_SUBST(VFS_INCLUDEDIR)
AC_SUBST(VFS_LIBS)

dnl Output files

AC_OUTPUT([
Makefile
gnome-vfs.spec
libgnomevfs/gnome-vfs-file-size.h
idl/Makefile
libgnomevfs-pthread/Makefile
libgnomevfs/Makefile
modules/Makefile
modules/extfs/Makefile
modules/extfs/ar
modules/extfs/cpio
modules/extfs/deb
modules/extfs/lha
modules/extfs/rar
modules/extfs/zip
modules/extfs/zoo
man/Makefile
man/gnome-vfs-mime.5
devel-docs/Makefile
devel-docs/gnome-vfs-tutorial/Makefile
po/Makefile.in
test/Makefile
test/vfs-run
doc/Makefile
])

chmod u+x test/vfs-run

dnl <= Configuration summary =>

echo "<= gnome-vfs configuration summary :"
dnl <= Profile support? =>
case "X$ENABLE_PROFILER" in
X1)
echo "
Enable profiler         : YES"
;;
*)
echo "
Enable profiler         : NO"
echo
;;
esac
case "$platform_gnome_2" in
Xyes)
echo "
GNOME platform          : 2.0"
;;
*)
echo "
GNOME platform          : 1.x"
;;
esac

dnl <= CFLAGS and LDFLAGS =>
echo "
CFLAGS                  : $CFLAGS
LDFLAGS                 : $LDFLAGS"
echo
