#
# Usage:
# sed -n -f device_labels_alsa.sed </usr/src/linux/include/linux/asound.h >device_labels_alsa.h
#
# This script generates i18n stub file with device_labels ALSA names.
# This script should be run by maintainer, if latest ALSA driver
# adds new device label. It implies appearing of new string
# in gnome-media.pot
#
:1
s/"/N_(@/1
s/"/@)/1
t1
s/@/"/g
/element types/,$d
# Others seems not to be used in recent gnome-media
/others/,$d
s/#define /           /
/SND_MIXER_VERSION/i/* device_labels_alsa.h\
 * This is a stub file, generated by device_labels_alsa.sed\
 * from linux/asound.h ALSA include. */\
\
#error This file is i18n stub file and cannot be included!\

/SND_MIXER_[IOGES]/,/}/p
