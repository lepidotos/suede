## (C) 1998 Stephan Kulow
## Modified by Tero Pulkkinen
#
# NOTE! IF YOU DO CHANGES HERE, CHECK IF YOU NEED TO MODIFY gnome--.m4 TOO!!!
#
AC_INIT(gnome--.spec.in)

#########################################################################
#  Version and initialization
#########################################################################
GNOMEMM_MAJOR_VERSION=1
GNOMEMM_MINOR_VERSION=2
GNOMEMM_MICRO_VERSION=2


#
# +1 : ? : +1  == new interface that does not break old one
# +1 : ? : 0   == new interface that breaks old one
#  ? : ? : 0   == no new interfaces, but breaks apps
#  ? :+1 : ?   == just some internal changes, nothing breaks but might work 
#                 better
# CURRENT : REVISION : AGE
LIBGNOMEMM_SO_VERSION=9:3:0
GNOMEMM_VERSION=$GNOMEMM_MAJOR_VERSION.$GNOMEMM_MINOR_VERSION.$GNOMEMM_MICRO_VERSION
GNOMEMM_RELEASE=$GNOMEMM_MAJOR_VERSION.$GNOMEMM_MINOR_VERSION
AC_DEFINE_UNQUOTED(GNOMEMM_MAJOR_VERSION, $GNOMEMM_MAJOR_VERSION)
AC_DEFINE_UNQUOTED(GNOMEMM_MINOR_VERSION, $GNOMEMM_MINOR_VERSION)
AC_DEFINE_UNQUOTED(GNOMEMM_MICRO_VERSION, $GNOMEMM_MICRO_VERSION)
AC_SUBST(GNOMEMM_VERSION)
AC_SUBST(GNOMEMM_RELEASE)
AC_SUBST(LIBGNOMEMM_SO_VERSION)

AC_CONFIG_AUX_DIR(scripts)

dnl For automake.
VERSION=$GNOMEMM_VERSION
PACKAGE=gnomemm

dnl Initialize automake stuff
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

dnl Specify a configuration file
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE
AM_ACLOCAL_INCLUDE(macros)

#########################################################################
#  Configure arguments 
#########################################################################
AC_ARG_ENABLE(voodoo,
[  --enable-voodoo    Use Voodoo Fried Chicken spell to compile Gtk-- (use this only when everything else fails)],
	ac_voodoo=yes
	AC_MSG_RESULT("Enabling Voodoo Fried Chicken spell")
)


#########################################################################
#  Environment Checks
#########################################################################
AC_PROG_CC
AC_PROG_CPP
AM_PROG_LEX
AC_PROG_YACC
AC_PROG_MAKE_SET

AC_CANONICAL_HOST

dnl AM_ENABLE_SHARED(yes)
dnl AM_ENABLE_STATIC(no)

AM_DISABLE_STATIC
dnl libtool does all checks with $CC, not with $CXX
dnl maybe this should be fixed in libtool, not here
AM_PROG_LIBTOOL

dnl On FreeBSD 3.0 (and perhaps some other systems) GNU m4 is
dnl called `gm4' where `m4' is the system's own m4.
AC_CHECK_PROGS(M4, gm4 m4, m4)

if test "$M4" = "m4"; then
  AC_MSG_CHECKING(whether m4 is GNU m4)
  if $M4 --version 2>/dev/null | grep '^GNU m4 ' >/dev/null ; then
	  AC_MSG_RESULT(yes)
  else
	  AC_MSG_RESULT(no)
	  if test "$host_vendor" = "sun"; then
		  AC_MSG_ERROR("SUN m4 does not work for building Gtk--. Please install GNU m4")
	  fi
  fi
fi

dnl Check that this is GNU m4 - if not, exit with error if this is a SUN
dnl

dnl We need Perl to build the docs
dnl
AC_CHECK_PROGS(PERL, perl5 perl, no)
if test "$PERL" = "no"; then
	AC_MSG_WARN([Perl5 is not installed : docs won't build])
fi

dnl Check for GNU make (stolen from gtk+/configure.in)
dnl 
AC_MSG_CHECKING(whether make is GNU Make)
if $ac_make --version 2>/dev/null | grep '^GNU Make ' >/dev/null ; then
	AC_MSG_RESULT(yes)
else
	AC_MSG_RESULT(no)
	if test "$host_vendor" = "sun" ; then
		AC_MSG_ERROR("SUN make does not work for building Gtk--. Please install GNU make")
	fi
fi

dnl we now require 1.2 gtk+ - 1.1 is not allowed because it doesnt work.
AM_PATH_GTK(1.2.7)

dnl these gen_h stuffs are here because we want more than one
dnl makefile have access to this variable.
dnl unfortunately placing these to here still leaves us two more
dnl places at src/gtk--/Makefile.am where each of these files must
dnl be listed
GNOME_GEN_H_FILES=`cd $srcdir/src ; ls *.gen_h | xargs echo`
AC_SUBST(GNOME_GEN_H_FILES)


dnl GNOME_INIT_HOOK executes $1 as soon as gnome-config is found, even
dnl if --without-gnome was given on the command line
dnl hack: only execute GNOME_INIT_HOOK if $with_gnome != no
dnl Mar20: fixed gnomemm_prefix to use prefix, instead of gnome_prefix.
dnl I sure hope it doesnt break anything.

dnl Had to add these two lines below because 'prefix' and
dnl 'exec_prefix' will be set to NONE at this point unless the user
dnl specified them with a --prefix/--exec_prefix option.
dnl - GL

test "x$prefix" = xNONE && prefix=$ac_default_prefix
test "x$exec_prefix" = xNONE && exec_prefix='${prefix}'

GNOME_INIT_HOOK(gnome_ok=yes,nofail)
if test "$gnome_ok" = yes; then

   CPPFLAGS="$CPPFLAGS `$GNOME_CONFIG --cflags gnomeui`"

   # These go into the Makefiles
   #
   #
   gnome_prefix=`gnome-config --prefix`

   # This is where gnomemmConf will be installed : it must be in the
   # same prefix as Gnome itself
   gnome_libdir='${gnome_prefix}/lib'

   # And this is where libgnomemm.(a|so) will be installed
   #
   gnomemm_prefix='${prefix}'
   gnomemm_libdir='${gnomemm_prefix}/lib'
   gnomemm_includedir='${gnomemm_prefix}/include'

   # These go into gnomemmConf.sh (actually they go into the Makefiles
   # too because automake isn't too subtle, but they aren't used)
   #
   GNOMEMM_LIBDIR="-L$prefix/lib $GNOME_LIBDIR"
   GNOMEMM_INCLUDEDIR="-I$prefix/lib/$PACKAGE/include"
   test "x$prefix" = "x/usr" || \
      GNOMEMM_INCLUDEDIR="$GNOMEMM_INCLUDEDIR -I$prefix/include"
   GNOMEMM_INCLUDEDIR="$GNOMEMM_INCLUDEDIR $GNOME_INCLUDEDIR"
   GNOMEMM_LIBS="-lgnomemm"
fi

AM_CONDITIONAL(GNOMEMM, test x$gnome_ok = xyes)

AC_SUBST(GNOME_LA_FILES)
AC_SUBST(gnome_prefix)
AC_SUBST(gnome_libdir)
AC_SUBST(gnomemm_prefix)
AC_SUBST(gnomemm_libdir)
AC_SUBST(gnomemm_includedir)
AC_SUBST(GNOMEMM_LIBS)
AC_SUBST(GNOMEMM_LIBDIR)
AC_SUBST(GNOMEMM_INCLUDEDIR)

AC_MSG_CHECKING(if html documentation and manual pages are generated)
AC_ARG_ENABLE(docs,
[  --enable-docs[=PKGS]    Turn on document generation [default=no]],
[case "${enableval}" in
  yes) ac_docgen=yes ;;
   no) ac_docgen=no ;;
    *) AC_MSG_ERROR(bad value ${enableval} for --enable-docs) ;;
esac],[ ac_docgen=no ])
#AM_CONDITIONAL(GTKMM_GENDOCS, test x$ac_docgen = xyes)
#AM_CONDITIONAL(GDKMM_GENDOCS, test x$ac_docgen = xyes) 
AC_MSG_RESULT([$ac_docgen])

dnl C++ compiler feature check start here
dnl moved the c++ compiler alittle to make the basename check work right..
AC_PROG_CXX
AC_LANG_CPLUSPLUS
GNOME_CXX_WARNINGS

AM_PATH_SIGC(1.0.3,,
[AC_MSG_ERROR([Libsigc++ is required to compile Gnome--. Please check config.log.])])

AM_PATH_GTKMM(1.2.5)
GTKMM_PROC=`$GTKMM_CONFIG --proc`
AC_SUBST(GTKMM_PROC)

#######################################################################
# C++ compiler feature check start here
#######################################################################
#AC_MSG_CHECKING(if C++ environment provides all required features)
#if test "x$config_error" = xyes ; then
#  AC_MSG_RESULT([no])
#  AC_MSG_ERROR([Your compiler is not powerful enough to compile gtk--. If it should be, see config.log for more information of why it failed.])
#fi
#AC_MSG_RESULT([yes])

#	gtk--draw/Makefile	
#	gtk--draw/src/Makefile	
#	gtk--draw/examples/Makefile	


AC_OUTPUT([Makefile \
        gnome--.spec \
        src/gnomemmConf.sh \

	src/Makefile \
	src/build_sources/Makefile \
	src/gnome--/Makefile
	src/gnome--/private/Makefile \

	examples/Makefile \
	examples/examples.conf \

	docgen/Makefile \
	docs/Makefile  \
	docs/gnome/Makefile \

        scripts/Makefile 
])
